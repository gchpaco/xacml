<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<html>
  <head>
    <title>SRFI 16: Syntax for procedures of variable arity</title>
  </head>

  <body>

<H1>Title</H1>

SRFI 16: Syntax for procedures of variable arity

<H1>Author</H1>

Lars T Hansen

<H1>Status</H1>
This SRFI is currently in ``final'' status.  To see an explanation of each status that a SRFI can hold, see <A HREF="http://srfi.schemers.org/srfi-process.html">here</A>.
You can access the discussion via <A HREF=mail-archive/maillist.html>the archive of the mailing list</A>.
<P><UL>
<LI>Received: 1999/11/01
<LI>Draft: 1999/11/06-2000/01/07
<LI>Final: 2000/03/10
</UL>

<H1>Abstract</H1>

CASE-LAMBDA, a syntax for procedures with a variable number of arguments,
is introduced.

<H1>Rationale</H1>

CASE-LAMBDA reduces the clutter of procedures that execute different
code depending on the number of arguments they were passed; it is 
a pattern-matching mechanism that matches on the number of arguments.
CASE-LAMBDA is available in some Scheme systems.

<p>While CASE-LAMBDA can be implemented as a macro using only facilities
available in R5RS Scheme, it admits considerable implementation-specific
optimization.


<H1>Specification</H1>

<dl>
<dt><a name="case-lambda"></a>
(CASE-LAMBDA &lt;clause&gt; ...)
<br> Syntax
<dd>
<p>Each &lt;clause&gt; should have the form (&lt;formals&gt; &lt;body&gt;), where 
   &lt;formals&gt; is a formal arguments list as for LAMBDA, cf section
   4.1.4 of the R5RS.  Each &lt;body&gt; is a &lt;tail-body&gt;, cf section
   3.5 of the R5RS.

<p>A CASE-LAMBDA expression evaluates to a procedure that accepts
   a variable number of arguments and is lexically scoped in the
   same manner as procedures resulting from LAMBDA expressions.  
   When the procedure is called with some arguments V1 .. Vk, then
   the first &lt;clause&gt; for which the arguments agree with &lt;formals&gt; 
   is selected, where agreement is specified as for the &lt;formals&gt; 
   of a LAMBDA expression.  The variables of &lt;formals&gt; are bound 
   to fresh locations, the values V1 .. Vk are stored in those 
   locations, the &lt;body&gt; is evaluated in the extended environment,
   and the results of &lt;body&gt; are returned as the results of the 
   procedure call.

<p>It is an error for the arguments not to agree with the &lt;formals&gt;
   of any &lt;clause&gt;.

<pre>
             (define plus
               (case-lambda 
                 (() 0)
                 ((x) x)
                 ((x y) (+ x y))
                 ((x y z) (+ (+ x y) z))
                 (args (apply + args))))

             (plus)                     --&gt; 0
             (plus 1)                   --&gt; 1
             (plus 1 2 3)               --&gt; 6

             ((case-lambda 
               ((a) a)
               ((a b) (* a b)))
              1 2 3)                    --&gt; error
</pre>
</dl>

<H1>Implementation</H1>

The following implementation is written in R5RS Scheme.  It is not
compatible with the IEEE Scheme standard because the IEEE standard does
not contain the high-level macro system.

<p>The implementation assumes that some top-level names defined by the
R5RS are bound to their original values.

<p>
<pre>
(define-syntax case-lambda
  (syntax-rules ()
    ((case-lambda 
      (?a1 ?e1 ...) 
      ?clause1 ...)
     (lambda args
       (let ((l (length args)))
         (case-lambda "CLAUSE" args l 
           (?a1 ?e1 ...)
           ?clause1 ...))))
    ((case-lambda "CLAUSE" ?args ?l 
      ((?a1 ...) ?e1 ...) 
      ?clause1 ...)
     (if (= ?l (length '(?a1 ...)))
         (apply (lambda (?a1 ...) ?e1 ...) ?args)
         (case-lambda "CLAUSE" ?args ?l 
           ?clause1 ...)))
    ((case-lambda "CLAUSE" ?args ?l
      ((?a1 . ?ar) ?e1 ...) 
      ?clause1 ...)
     (case-lambda "IMPROPER" ?args ?l 1 (?a1 . ?ar) (?ar ?e1 ...) 
       ?clause1 ...))
    ((case-lambda "CLAUSE" ?args ?l 
      (?a1 ?e1 ...)
      ?clause1 ...)
     (let ((?a1 ?args))
       ?e1 ...))
    ((case-lambda "CLAUSE" ?args ?l)
     (error "Wrong number of arguments to CASE-LAMBDA."))
    ((case-lambda "IMPROPER" ?args ?l ?k ?al ((?a1 . ?ar) ?e1 ...)
      ?clause1 ...)
     (case-lambda "IMPROPER" ?args ?l (+ ?k 1) ?al (?ar ?e1 ...) 
      ?clause1 ...))
    ((case-lambda "IMPROPER" ?args ?l ?k ?al (?ar ?e1 ...) 
      ?clause1 ...)
     (if (>= ?l ?k)
         (apply (lambda ?al ?e1 ...) ?args)
         (case-lambda "CLAUSE" ?args ?l 
           ?clause1 ...)))))
</pre>

<H1>Copyright</H1>
Copyright (C) Lars T Hansen (1999). All Rights Reserved. 
<p>
This document and translations of it may be copied and furnished to
others, and derivative works that comment on or otherwise explain it
or assist in its implementation may be prepared, copied, published and
distributed, in whole or in part, without restriction of any kind,
provided that the above copyright notice and this paragraph are
included on all such copies and derivative works. However, this
document itself may not be modified in any way, such as by removing
the copyright notice or references to the Scheme Request For
Implementation process or editors, except as needed for the purpose of
developing SRFIs in which case the procedures for copyrights defined
in the SRFI process must be followed, or as required to translate it
into languages other than English.
<p>
The limited permissions granted above are perpetual and will not be
revoked by the authors or their successors or assigns.
<p>
This document and the information contained herein is provided on an
"AS IS" basis and THE AUTHOR AND THE SRFI EDITORS DISCLAIM ALL
WARRANTIES, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO ANY
WARRANTY THAT THE USE OF THE INFORMATION HEREIN WILL NOT INFRINGE ANY
RIGHTS OR ANY IMPLIED WARRANTIES OF MERCHANTABILITY OR FITNESS FOR A
PARTICULAR PURPOSE.


    <hr>
    <address>Editor: <a href="mailto:srfi-editors@schemers.org">Mike Sperber</a></address>
<!-- Created: Tue Sep 29 19:20:08 EDT 1998 -->
<!-- hhmts start -->
Last modified: Thu Aug 14 16:53:15 CDT 2003
<!-- hhmts end -->
  </body>
</html>
